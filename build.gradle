plugins {
	id 'java'
	id 'application'
	id 'org.openjfx.javafxplugin' version '0.0.10'
	id 'org.beryx.jlink' version '2.24.0'
}

group 'edu.missouristate'
version '0.1.0'
sourceCompatibility = '1.11'

sourceSets {
    main {
        java {
            srcDir 'src/main/java'
        }
    }
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
    mavenCentral()
}

java {
     modularity.inferModulePath = true
}

application {
     mainModule = 'edu.missouristate.taschedulegenerator' // name defined in module-info.java
     mainClass = 'edu.missouristate.taschedulegenerator.Main'
}

javafx {
    version = "11"
    modules = [ 'javafx.controls', 'javafx.fxml' ]
}

jlink {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'TA Schedule Generator'
        jvmArgs = ['-Djdk.gtk.version=2'] // required due to a bug in Java: https://github.com/javafxports/openjdk-jfx/issues/175
    }
	jpackage {
		installerType = 'msi'
        installerOptions = [
            '--name', 'TA Schedule Generator',
            '--win-per-user-install', '--win-dir-chooser',
            '--win-menu', '--win-shortcut',
            '--win-console' // TODO: Remove this in release
        ]
        imageOptions += ['--icon', 'src/main/resources/icon.ico']
    }
}

dependencies {
	compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.20'
	annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.20'
	implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.12.5'
	implementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: '2.12.5'
	implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.12.0'
	implementation group: 'org.apache.poi', name: 'poi', version: '5.1.0'
	implementation group: 'org.apache.poi', name: 'poi-ooxml', version: '5.1.0'
}
